CLOSE

;####################################################
; Custom motion program to release rail clamp (brake)
; before move and then apply it at the end.
;
; Author: Andrew Wilson
;
; Used variables: Q71..Q79, Q81..Q89
; M0 and M32 of GPIO
; M4950 and M4951 of user memory
;
; TODO: Reference these M variables by memory address
; 
; You should do a $$$ or type the following the first time you load this prog.
; If you type the commands in, ignore any errors. If you $$$, PLC1 will do it
; automatically for you at startup:
;&16 DEFINE LOOKAHEAD 50,10
;&15 DEFINE LOOKAHEAD 50,10
;&14 DEFINE LOOKAHEAD 50,10
;&13 DEFINE LOOKAHEAD 50,10
;&12 DEFINE LOOKAHEAD 50,10
;&11 DEFINE LOOKAHEAD 50,10
;&10 DEFINE LOOKAHEAD 50,10
;&9 DEFINE LOOKAHEAD 50,10
;&8 DEFINE LOOKAHEAD 50,10
;&7 DEFINE LOOKAHEAD 50,10
;&6 DEFINE LOOKAHEAD 50,10
;&5 DEFINE LOOKAHEAD 50,10
;&4 DEFINE LOOKAHEAD 50,10
;&3 DEFINE LOOKAHEAD 50,10
;&2 DEFINE LOOKAHEAD 50,10
;####################################################

;This program defines the program needed to do a move using the motor record
;on a CS axis
;to do a move we need to do &{CS}R
;to stop a move we need to do &{CS}A
;demand values are in &{CS}Q71..79
;move time is &{CS}Q70 (see details below)
;The following are set by the motor record:
;Isx87 = Acceleration
;Isx88 = S curve
;Isx89 = Feedrate : no longer used since always do timed moves now

; Set some defaults for all CS axes
i5213,15,100=10   ; segmentation time (needed for lookahead)
i5220,15,100=50   ; lookahead length (needed to limit max velocity to max set in CS)
i5250,15,100=1    ; Enable kinematics

OPEN PROG 11
CLEAR
LINEAR
ABS
FRAX(A,B,C,U,V,W,X,Y,Z)

#define AIROK M0     ; Assume 1 is OK and 0 is not OK
#define AIRVALVE M32 ; Assume 1 is open and 0 is closed
#define STATE M4950  ; Enum to report current state of air control
; Enum values:
#define STATE_IDLE  0       ; Idle
#define STATE_OPEN_WAIT 1   ; Waiting after opening valve
#define STATE_OPEN_ERROR; 2 ; Air didn't come on when expected
#define STATE_MOVING 3      ; Move in progress
#define STATE_CLOSE_WAIT 4  ; Waiting after closing valve
#define STATE_CLOSE_ERROR 5 ; Air didn't go off when expected

#define AirControlEnabled M4951 ; Flag to say whether we will control the air ourselves or not.
#define AIRDELAY 1000 ; ms delay of solenoid valve

STATE = STATE_IDLE

; Only open valve if we have been asked to control it
; through this program.
if AirControlEnabled
	; Open valve
	AIRVALVE=1
	STATE=STATE_OPEN_WAIT
	; Wait for brake to be released
	DWELL AIRDELAY
endif

; If the air is on, then it is OK to move
if AIROK
	STATE=STATE_MOVING
	; Q70 is used to set the move time.
	; The pmac driver sets Q70 based on the PV $(P):$(CS):CsMoveTime for deferred moves
	; and based on the motor record velocity (and move distance) for individual cs motor moves
	TM(Q70)
	A(Q71)B(Q72)C(Q73)U(Q74)V(Q75)W(Q76)X(Q77)Y(Q78)Z(Q79)
	DWELL0
else
	; If the air is not on, we do not do the move
	; and we set the error flag
	STATE=STATE_OPEN_ERROR
endif

; Turn off the air at the end to apply the brake
if AirControlEnabled
	; Close the valve
	AIRVALVE=0
	STATE=STATE_CLOSE_WAIT
	
	; Wait for the brake to apply
	DWELL AIRDELAY

	if AIROK
		; Air is still on; should not be
		STATE=STATE_CLOSE_ERROR
	endif
CLOSE
